
apply plugin: 'jetty' // includes java, war

war.version = ''
war.baseName = 'music'
war.webXml = file('src/web.xml')

sourceSets.main.java.srcDirs 'src'
sourceSets.test.java.srcDirs 'test'
sourceSets.main.resources.srcDirs 'config'

configurations.compile {
    exclude group: 'javax.transaction'
} 

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.hibernate:hibernate:3.2.4.ga'
    compile 'javax.servlet:servlet-api:2.4'

    testCompile 'junit:junit:3.8.1'

    runtime files('../lib/jta-1.1.jar')
    runtime 'hsqldb:hsqldb:1.8.0.7'
}

httpPort=8088
stopPort=8089
stopKey='codeRed'

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(runWarTest)) {
        jettyRun.daemon = true
        jettyRunWar.daemon = true
    }
}

task runWarTest(dependsOn: jettyRunWar) << {
    URL url = new URL("http://localhost:${httpPort}/music/MusicServ")
    println "\n\n" + url.text + "\n"
    jettyStop.execute()
}

